01 'Initialize Program
02 RANDOMIZE TIMER
10 DEF FNSADD!(PSTRING!)=PEEK(PSTRING! + 2) + 256 * PEEK(PSTRING + 3)
15 DEF FNUCASE$(ARG1%)=CHR$(ARG1% + 32 * (ARG1%>=97))
17 DEF FNMAPX(X%)=(X%-16) \ 16
18 DEF FNMAPY(Y%)=(Y%-40) \ 8
20 CLS: SOUND ON: KEY OFF: SCREEN 3,0,0:PLAY "MBO3","MBO3","MBO3"
22 BOXW%=16:BOXH%=8
25 DIM BLIT%(50), PBLIT%(50), BBLIT%(20), PBBLIT%(20), BLBLIT%(36)
30 RUNNING%=1:STARTED%=0:BALLS%=0:COLLIDED%=0:PSIZE%=0
32 BX%=0:BY%=0:PX%=0:PY%=0:PBX%=0:PBY%=0:PPX%=0:PPY%=0
33 KEYNUM%=0:DBX%=0:DBY%=0:OVER%=0:CLWALL%=0:CRWALL%=0:CTWALL%=0:CBWALL%=0
34 CPLAYER=0:CBLOCK%=0:TBLOCKS%=0:DIM BLOCKS%(7,7)
40 CYCLES%=0:SCOREUPDATED%=0:SCORE%=0
45 SCORE$ = "SCORE:00000  BALLS:0"
47 scorePtr! = FNSADD!(VARPTR(SCORE$))
48 IF(PEEK(scorePtr!)<>83) THEN PCBASIC%=-1 ELSE PCBASIC%=0
50 'Trap Esc key with different keyboard states
55 KEY 15, CHR$(0) + CHR$(1) 'ESC key
60 ON KEY(15) GOSUB 50000
65 KEY(15) ON
70 KEY 16, CHR$(&H20) + CHR$(1) 'Num Lock state + ESC key
75 ON KEY(16) GOSUB 50000
80 KEY(16) ON
85 KEY 17, CHR$(&H60) + CHR$(1) 'Num Lock & Caps lock state + ESC key
90 ON KEY(17) GOSUB 50000
95 KEY(17) ON
97 KEY 18, CHR$(&H40) + CHR$(1)'ESC key 'Caps Lock state + ESC key
98 ON KEY(18) GOSUB 50000
99 KEY(18) ON
100 'Game Loop
105 innerTick% = 0
108 ON TIMER(1) GOSUB 310
110 WHILE (RUNNING%=1)
120   IF(NOT STARTED) THEN GOSUB 400
130   WHILE(innerTick% < 3)
140     GOSUB 30000 'Update Score
150     KEYNUM%=INP(96): GOSUB 600 'Handle keypress
155     IF(JOYSTICK%) THEN GOSUB 4000 'Handle joystick
160     GOSUB 800 'Move BALL
170     GOSUB 5000 'Detect Collision
180     GOSUB 6000 'Handle Collision
190     IF(OVER%) THEN GOSUB 50000:GOTO 295 'Query Terminate
250     GOSUB 3000 'Render Ball 
260     GOSUB 2000 'Render Player
270     innerTick% = innerTick% + 1
275     totalTicks! = totalTicks! + 1
280   WEND
285   IF(PCBASIC%) THEN dummy$ = INKEY$ 'Clear keyboard buffer
290   CYCLES%=1: GOSUB 40000 'Wait 1/20th of a second
295   innerTick% = 0 
300 WEND
310 totalSeconds% = totalSeconds% + 1
330 RETURN
400 'BEGIN GAME
410 STARTED = NOT STARTED:TBLOCKS%=64
420 BALLS%=5:SCORE%=0:SCOREUPDATED%=-1:PSIZE%=24:PY%=184:PX%=68:BX%=77:BY%=128
421 PBX%=BX%:PBY%=BY%:PPX%=PX%:PPY%=PY%:DBX%=0:DBY%=4+2*PCBASIC%:OVER%=0
422 CLWALL%=0:CTWALL%=0:CBLOCK%=0:CBWALL%=0:CRWALL%=0:CPLAYER%=0:COLLIDED%=0
425 COLOR 14
430 LOCATE 12, 1: PRINT "Breakout for PCjr"
440 LOCATE 14, 1: PRINT "Key instructions:"
450 LOCATE 16, 1: PRINT "Left arrow <--"
460 LOCATE 17, 1: PRINT "Right arrow -->"
470 LOCATE 18, 1: PRINT "ESC stops playing"
472 LOCATE 21, 1: PRINT "Press J for joystick"
475 LOCATE 25, 1: PRINT "Press any key...";
476 K$=INKEY$
480 WHILE(NOT (K$<>"" OR (JOYSTICK% AND STRIG(0))))
481   K$=INKEY$:IF(K$<>"") THEN KEYNUM%=ASC(K$) ELSE KEYNUM%=0
482   IF(FNUCASE$(KEYNUM%)="J") THEN GOSUB 20000
483 WEND
485 LINE(0,0)-(159,199),0,BF 'Clear screen using graphics
490 GOSUB 500
496 totalSeconds%=0:totalTicks!=0
497 TIMER ON
499 RETURN
500 'Draw Initial Board
510 GOSUB 1000 'Render Board
525 LINE(PX%,PY%)-(PX%+PSIZE%-1,PY%+7),6,BF
530 LINE(BX%,BY%)-(BX%+3,BY%+7),15,BF
575 GET(PX%, PY%)-(PX%+PSIZE%-1,PY%+7),BLIT%
576 GET(BX%,BY%)-(BX%+3,BY%+7),BBLIT%
577 PBLIT%(0)=BLIT%(0):PBLIT%(1)=BLIT%(1)
578 PBBLIT%(0)=BBLIT%(0):PBBLIT%(1)=BBLIT%(1)
579 GET(16,32)-(31,39),BLBLIT% 'Fill with background
580 FOR I%=0 TO 7
581   FOR J%=0 TO 7
582     BLOCKS%(I%,J%)=-1
583   NEXT J%
584 NEXT I%
599 RETURN
600 'Handle Keyboard Proc
650 IF (KEYNUM%=&H4B) THEN GOSUB 700 'Go Left
660 IF (KEYNUM%=&H4D) THEN GOSUB 760 'Go Right
699 RETURN
700 'Go Left
710 IF(PX%>16) THEN PX% = PX% - 2 + 2 * (PCBASIC%=0) ELSE PX%=16
750 RETURN
760 'Go Right
770 IF(PX%<120) THEN PX% = PX% + 2 - 2 * (PCBASIC%=0) ELSE PX%=120
790 RETURN
800 'Move Ball
810 BX%=BX% + DBX%
820 BY%=BY% + DBY%
830 RETURN
1000 'Render Board Proc
1010 R1C%=INT(RND*15)+1:R2C%=INT(RND*15)+1:R3C%=INT(RND*15)+1
1020 R4C%=INT(RND*15)+1
1025 IF(R1C%=R2C% OR R1C%=R3C% OR R1C%=R4C%) THEN GOTO 1010
1030 IF(R2C%=R3C% OR R2C%=R4C% OR R3C%=R4C%) THEN GOTO 1010
1050 GOSUB 1500 'Render Frame
1060 LINE(16, 40)-(143, 46), R1C%, BF
1070 LINE(16, 48)-(143, 54), R1C%, BF
1080 LINE(16, 56)-(143, 62), R2C%, BF
1090 LINE(16, 64)-(143, 70), R2C%, BF
1100 LINE(16, 72)-(143, 78), R3C%, BF
1110 LINE(16, 80)-(143, 86), R3C%, BF
1120 LINE(16, 88)-(143, 94), R4C%, BF
1130 LINE(16, 96)-(143, 102), R4C%, BF
1200 RETURN
1500 'Render Frame Proc
1510 LINE(0, 16)-(159, 23),7,BF
1520 LINE(0, 24)-(15, 191),7,BF
1530 LINE(144, 24)-(159, 191),7,BF
1600 RETURN
2000 'Render Player Proc
2010 IF(PPX%=PX%) THEN GOTO 2400
2020 PUT(PPX%, PPY%),PBLIT%,PSET
2030 PUT(PX%, PY%),BLIT%,PSET
2040 PPX%=PX%
2400 RETURN
3000 'Render Ball Proc
3100 IF(PBX%=BX% AND PBY%=BY%) THEN GOTO 3400
3130 PUT(PBX%,PBY%),PBBLIT%,PSET
3140 PUT(BX%,BY%),BBLIT%,PSET
3390 PBX%=BX%:PBY%=BY%
3400 RETURN
4000 'Handle joystick proc
4010 joymove%=STICK(0)
4030 IF(joymove%<=JOYCENTER%-10) THEN GOSUB 700
4040 IF(joymove%>=JOYCENTER%+10) THEN GOSUB 760
4100 RETURN
5000 'Detect Collision Proc
5010 IF(BY%>(PY%-7)) THEN GOTO 5100
5020 IF(BY%<=23) THEN CTWALL%=-1:GOTO 5390
5030 IF(BX%>139) THEN CRWALL%=-1:GOTO 5390
5040 IF(BX%<16) THEN CLWALL%=-1:GOTO 5390
5050 IF(BY%<102 AND BY%>38) THEN CBLOCK%=-1:GOTO 5390
5060 GOTO 5400 'No Collision
5100 IF(BX%>(PX%-4) AND BX%<(PX%+PSIZE%)) THEN CPLAYER%=-1:GOTO 5390
5200 IF(BY%>PY%) THEN CBWALL%=-1:GOTO 5390 ELSE GOTO 5400
5390 COLLIDED%=-1
5400 RETURN
6000 'Handle Collision Proc
6010 IF(NOT COLLIDED%) THEN GOTO 6400
6015 COLOR 14
6020 IF(CBWALL%)THEN BALLS%=BALLS%-1:LOCATE 25,1:PRINT "Missed ball!";:GOTO 6300
6030 IF(CTWALL%)THEN DBY%=-DBY%:BY%=BY%+DBY%:GOTO 6390
6040 IF(CLWALL%)THEN DBX%=-DBX% - 2*(DBX%=0):BX%=16:GOTO 6390
6050 IF(CRWALL%)THEN DBX%=-DBX% + 2*(DBX%=0):BX%=140:GOTO 6390
6080 IF(CBLOCK%)THEN GOTO 6100
6090 IF(CPLAYER%)THEN GOTO 6200
6100 'Handle Block Collision
6105 mapx%=FNMAPX(BX%):mapx2%=FNMAPX(BX%+3):mapy%=FNMAPY(BY%)
6110 IF(NOT BLOCKS%(mapx%,mapy%) AND NOT BLOCKS%(mapx2%,mapy%))THEN GOTO 6390
6112 IF(BLOCKS%(mapx%,mapy%) AND BLOCKS%(mapx2%,mapy%) AND mapx%<>mapx2%)THEN SCORE%=SCORE%+10
6115 IF(NOT BLOCKS%(mapx%,mapy%)) THEN GOTO 6150
6120 BLOCKS%(mapx%,mapy%)=0
6130 SCORE%=SCORE%+(8-mapy%)*5
6135 TBLOCKS%=TBLOCKS%-1
6140 PUT(16+(mapx%*16),40+(mapy%*8)),BLBLIT%,PSET
6150 IF(NOT BLOCKS%(mapx2%,mapy%)) THEN GOTO 6170
6152 BLOCKS%(mapx2%,mapy%)=0
6155 PUT(16+(mapx2%*16),40+(mapy%*8)),BLBLIT%,PSET
6160 SCORE%=SCORE%+(8-mapy%)*5 
6165 TBLOCKS%=TBLOCKS%-1
6170 DBY%=-DBY%:BY%=BY%+DBY%
6175 NOISE INT(RND*2),15,1
6180 SCOREUPDATED%=-1 'Refresh Score Display
6185 IF(TBLOCKS%<1) THEN TBLOCKS%=64:PUT(PBX%,PBY%),PBBLIT%,PSET:GOSUB 1000
6186 IF(TBLOCKS%<>64) THEN GOTO 6390
6187 PBX%=78:PBY%=184
6190 COLOR 14:LOCATE 25,1:PRINT"Complete!";
6192 FOR I%=0 TO 7:FOR J%=0 TO 7:BLOCKS%(I%,J%)=-1:NEXT J%:NEXT I%
6195 CYCLES%=20:GOSUB 40000:GOTO 6300
6200 'Handle Player Collision
6210 DBY%=-DBY%:BY%=BY%+DBY%
6220 IF(BX%<=PX%) THEN DBX%=-2:GOTO 6290
6230 IF(BX%<(PX%-2+PSIZE%\2)) THEN DBX%=-1:GOTO 6290
6235 IF(BX%=(PX%-2+PSIZE%\2)) THEN DBX%=0:GOTO 6290
6240 IF(BX%>=(PX%+PSIZE%-5)) THEN DBX%=2:GOTO 6290
6250 IF(BX%<(PX%+PSIZE%-5)) THEN DBX%=1
6290 GOTO 6390
6300 'Handle Player Missed Ball
6301 GOSUB 1500 'Render Frame
6305 IF(BALLS%<=0) THEN OVER%=1:SCOREUPDATED%=-1:GOSUB 30000:GOTO 6390
6307 CYCLES%=20:GOSUB 40000 'Delay some
6310 COLOR 14:LOCATE 25,1:PRINT "Press a key or fire";
6315 IF(PCBASIC%=0) THEN DEF SEG=0:POKE 1050,PEEK(1052):DEF SEG
6320 WHILE(NOT (INKEY$<>"" OR (JOYSTICK% AND STRIG(0)))):WEND
6330 LOCATE 25,1:PRINT SPACE$(20);
6340 BX%=77:BY%=128:PY%=184:PX%=68:DBX%=0:DBY%=4+2*PCBASIC%
6350 SCOREUPDATED%=-1:GOSUB 30000 'Refresh Score1
6390 COLLIDED%=0:CBLOCK%=0:CPLAYER%=0:CBWALL%=0:CLWALL%=0:CTWALL%=0:CRWALL%=0
6400 RETURN
20000 'Align joystick proc
20005 LOCATE 25,1:PRINT "Joystick <- -> fire";
20010 I%=0:maxStick%=-32000:minStick%=32000
20020 WHILE((NOT STRIG(0)) AND I%<10000)
20025   sample%=STICK(0)
20030   IF(sample%<minStick%) THEN minStick%=sample%
20035   IF(sample%>maxStick%) THEN maxStick%=sample%
20040   I%=I%+1
20045 WEND
20050 JOYLEFT%=minStick%:JOYRIGHT%=maxStick%
20055 LOCATE 25,1:PRINT SPACE$(20);
20060 LOCATE 25,1:PRINT "Center and fire";
20070 FOR I%=0 TO 1000:sample%=STRIG(0):NEXT I%
20080 I%=0:WHILE((NOT STRIG(0)) AND I%<10000):I%=I%+1:JOYCENTER%=STICK(0):WEND
21055 LOCATE 25,1:PRINT "L";JOYLEFT%;" C";JOYCENTER%;" R";JOYRIGHT%;
21056 CYCLES%=40:GOSUB 40000
21060 IF(JOYLEFT%<JOYCENTER%) AND (JOYCENTER%<JOYRIGHT%) THEN JOYSTICK%=-1 ELSE JOYSTICK%=0
21070 LOCATE 25,1
21080 IF(JOYSTICK%)THEN PRINT "Success press fire  ";
21090 IF(NOT JOYSTICK%) THEN PRINT "Joystick not detected";
21095 CYCLES%=20:GOSUB 40000
22000 RETURN
30000 'Score Proc
30100 IF (NOT SCOREUPDATED%) THEN GOTO 34999
30150 IF (SCORE% > 32766) THEN SCORE%=0 'You flipped it :-)
30200 scorePtr! = FNSADD!(VARPTR(SCORE$))
30210 IF(PCBASIC%) THEN GOTO 31000 'Fallback
30300 POKE scorePtr! + 19, BALLS% + &H30
30310 POKE scorePtr! + 10, (SCORE% MOD 10) + &H30
30320 POKE scorePtr! +  9, ((SCORE% \ 10) MOD 10) + &H30
30330 POKE scorePtr! +  8, ((SCORE% \ 100) MOD 10) + &H30
30340 POKE scorePtr! +  7, ((SCORE% \ 1000) MOD 10) + &H30
30350 POKE scorePtr! +  6, ((SCORE% \ 10000) MOD 10) + &H30
30360 LOCATE 1,1: COLOR 7: PRINT SCORE$;
30370 GOTO 34000 'Quick Print Successful
31000 'Fallback to slow logic
31010 tmpScore$ = MID$(STR$(SCORE%), 2) : tmpBall$ = MID$(STR$(BALLS%), 2)
31020 padZero$ = ""
31030 IF (SCORE%<10) THEN padZero$ = "0000"
31040 IF (SCORE%<100 AND padZero$="") THEN padZero$ = "000"
31050 IF (SCORE%<1000 AND padZero$="") THEN padZero$ = "00"
31060 IF (SCORE%<10000 AND padZero$="") THEN padZero$ = "0"
31070 LOCATE 1,1: COLOR 7
31100 PRINT USING "&&&&&";"SCORE:";padZero$;tmpScore$;"  BALLS:";tmpBall$;
34000 SCOREUPDATED% = NOT SCOREUPDATED%
34999 RETURN
40000 'Tick Proc
40010 tickCount% = 0
40020 track! = TIMER
40030 WHILE (tickCount% < CYCLES%)
40040   IF (track! = TIMER) THEN GOTO 40080
40050   tickCount% = tickCount% + 1
40060   track! = TIMER
40080 WEND
40090 RETURN
50000 'Termination proc
50100 KEY(15) OFF: KEY(16) OFF: KEY(17) OFF: KEY(18) OFF
51000 LOCATE 25, 1: COLOR 14: PRINT "STOP PLAYING(Y/N)?";
52000 K$=INKEY$
52010   IF (K$<>"") THEN KEYNUM%=ASC(K$) ELSE KEYNUM%=0
52100   IF(FNUCASE$(KEYNUM%)="Y") THEN GOTO 55000
52150   IF(FNUCASE$(KEYNUM%)="N") THEN GOTO 52200
52180 GOTO 52000
52200 KEY(15) ON: KEY(16) ON: KEY(17) ON: KEY(18) ON
52210 VIEW:CLS
53000 STARTED = NOT STARTED: RETURN
55000 CLS: SCREEN 0,0,0: SOUND OFF: KEY ON: WIDTH 80: END
